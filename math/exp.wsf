# Copyright (c) 2021 Andrew Archibald
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

# exp returns x raised to the power of y, using exponentiation by
# squaring. If y <= 0, 1 is returned.
# (x y -- x**y)
exp:
    ^ 1   j< .exp_one   # y <= 0
.exp:
    ^ 1   j= .exp_x     # y == 1
    ^ 2 % jz .exp_even  # y%2 == 0
# .exp_odd:
    # exp(x**2, (y-1)/2) * x
    ^1 ^ * swap
    1 - 2 /
    call .exp
    *
    ret
.exp_even:
    # exp(x**2, y/2)
    swap ^ * swap
    2 /
    jmp .exp
.exp_one:
    2drop 1 ret
.exp_x:
    drop ret

# TODO
# (x y m -- (x**y)%m)
modexp:
    -999 3slide ret

# tetrate returns the tetration of x and y, that is, y copies of x
# combined by exponentiation, right-to-left. If y <= 0, 1 is returned.
# (x y -- x^^y)
tetrate:
    1
.tetrate_loop:
    swap 1 - swap
    ^1 jn .tetrate_done
    ^2 swap call exp
    jmp .tetrate_loop
.tetrate_done:
    2slide ret

# pentate returns the pentation of x and y, that is, y copies of x
# combined by tetration, right-to-left. If y <= 0, 1 is returned.
# (x y -- x^^^y)
pentate:
    1
.pentate_loop:
    swap 1 - swap
    ^1 jn .pentate_done
    ^2 swap call tetrate
    jmp .pentate_loop
.pentate_done:
    2slide ret
