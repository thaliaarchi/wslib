# Copyright (c) 2021 Andrew Archibald
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

# is_space reports whether the character is a space character as defined
# by Unicode's White Space property.
# (ch -- ?)
is_space:
    ^ 256 j< .is_space_latin1
    # 5760 U+1680 Ogham space mark
    ^ 5760 j= .is_space_true
    # 8192 U+2000 En quad
    # 8193 U+2001 Em quad
    # 8194 U+2002 En space
    # 8195 U+2003 Em space
    # 8196 U+2004 Three-per-em space
    # 8197 U+2005 Four-per-em space
    # 8198 U+2006 Six-per-em space
    # 8199 U+2007 Figure space
    # 8200 U+2008 Punctuation space
    # 8201 U+2009 Thin space
    # 8202 U+200A Hair space
    ^ 8192 j< .is_space_false
    ^ 8203 j< .is_space_true
    # 8232 U+2028 Line separator
    ^ 8232 j= .is_space_true
    # 8233 U+2029 Paragraph separator
    ^ 8233 j= .is_space_true
    # 8239 U+202F Narrow no-break space
    ^ 8239 j= .is_space_true
    # 8287 U+205F Medium mathematical space
    ^ 8287 j= .is_space_true
    # 12288 U+3000 Ideographic space
    ^ 12288 j= .is_space_true
    jmp .is_space_false
.is_space_latin1:
    # 9   U+0009 Character tabulation
    # 10  U+000A Line feed
    # 11  U+000B Line tabulation
    # 12  U+000C Form feed
    # 13  U+000D Carriage return
    ^ 9   j< .is_space_false
    ^ 14  j< .is_space_true
    # 32  U+0020 Space
    ^ 32  j= .is_space_true
    # 133 U+0085 Next line
    ^ 133 j= .is_space_true
    # 160 U+00A0 No-break space
    ^ 160 j= .is_space_true
.is_space_false:
    drop 0 ret
.is_space_true:
    drop 1 ret
