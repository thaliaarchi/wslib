# Copyright (c) 2021 Andrew Archibald
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

    "@types/bool" import
    "@types/int"  import

# print_quote prints a quoted character with escape sequences for
# ASCII control characters.
# (c -- )
print_quote:
    '\'' printc
    call print_escape
    '\'' printc
    ret

# print_escape prints a character with escape sequences for ASCII
# control characters.
# (c -- )
print_escape:
    ^ ' '  j< .print_escape_control
    ^ '\'' j= .print_escape_slash
    ^ '\\' j= .print_escape_slash
.print_escape_ret:
    printc
    ret
.print_escape_slash:
    '\\' printc
    jmp .print_escape_ret
.print_escape_control:
    '\\' printc
    ^ '\a' j= .print_escape_a
    ^ '\b' j= .print_escape_b
    ^ '\t' j= .print_escape_t
    ^ '\n' j= .print_escape_n
    ^ '\v' j= .print_escape_v
    ^ '\f' j= .print_escape_f
    ^ '\r' j= .print_escape_r
    ^ '\e' j= .print_escape_e
    'x' printc
    16 swap 2
    call int.print_base_pad_0
    ret
.print_escape_a:
    'a' jmp .print_escape_ret
.print_escape_b:
    'b' jmp .print_escape_ret
.print_escape_t:
    't' jmp .print_escape_ret
.print_escape_n:
    'n' jmp .print_escape_ret
.print_escape_v:
    'v' jmp .print_escape_ret
.print_escape_f:
    'f' jmp .print_escape_ret
.print_escape_r:
    'r' jmp .print_escape_ret
.print_escape_e:
    'e' jmp .print_escape_ret

# read_expect reads the next char and returns whether it equals ch.
# Clobbers the given address.
# (addr ch -- ok?)
read_expect:
    swap ^ readc retrieve = ret
