# Copyright (c) 2021 Andrew Archibald
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

    "@string" import

    "Shift: " prints
    0 ^ readi retrieve
    "Plain: " prints
    0 call string.read_line
    0 ^1 ^3 call caesar
    0 ^1 call string.println
    0 ^1 ^3 neg call caesar
    0 ^1 call string.println
    2drop end

# caesar shifts the alphabetic characters in the string starting at addr
# in-place by shift.
# (addr len shift -- )
caesar:
    ^2 ^2 + 1-
    ^3
.caesar_loop:
    2dup j< .caesar_ret
    ^ retrieve
    ^ 'A' j< .caesar_other
    ^ '[' j< .caesar_upper
    ^ 'a' j< .caesar_other
    ^ '{' j< .caesar_lower
.caesar_other:
    drop
.caesar_continue:
    1+
    jmp .caesar_loop
.caesar_upper:
    'A' - ^3 + 26% 'A' +
    ^1 swap store
    jmp .caesar_continue
.caesar_lower:
    'a' - ^3 + 26% 'a' +
    ^1 swap store
    jmp .caesar_continue
.caesar_ret:
    5drop ret

# rot13 shifts the alphabetic characters in the string starting at addr
# in-place by 13.
# (addr len -- )
rot13:
    13 jmp caesar
