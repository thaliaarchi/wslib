# Copyright (c) 2021 Andrew Archibald
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

# clear sets all values in the array to 0.
# (addr len -- )
clear:
    0 jmp set_all

# set_all sets all values in the array to x.
# (addr len x -- )
set_all:
    swap ^2 + 1- ^2
.set_all_loop:
    2dup j< .set_all_ret
    ^ ^3 store
    1+
    jmp .set_all_loop
.set_all_ret:
    4drop ret

# replace_all replaces all instances of x in the array to y.
# (addr len x y -- count)
replace_all:
    ^3 ^3 + 1-
    0 # count
    ^5
.replace_all_loop:
    ^2 ^1 j< .replace_all_ret
    # addr len x y max count addr
    ^ retrieve ^5 j= .replace_all_store
.replace_all_next:
    1+
    jmp .replace_all_loop
.replace_all_ret:
    swap 6slide ret
.replace_all_store:
    ^ ^4 store
    swap 1+ swap
    jmp .replace_all_next

# copy copies len values from the array at addr1 to the array at addr2.
# (addr1 addr2 len -- )
copy:
    ^2 + 1- # addr1+len-1
    ^1 ^3   # i, j = addr1, addr2
.copy_loop:
    ^2 ^1 j< .copy_ret # i >= addr1+len
    2dup retrieve store
    1+ swap 1+ swap
    jmp .copy_loop
.copy_ret:
    5drop ret

# swap swaps the values at addr1 and addr2.
# (addr1 addr2)
swap:
    swap ^ retrieve swap ^2 retrieve
    store store ret
