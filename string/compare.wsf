# Copyright (c) 2021-2022 Thalia Archibald
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

    "@bool" import

# equals returns whether the two strings are equal.
# (addr1 len1 addr2 len2 -- ?)
equals:
    ^2 ^1 j= .equals_len_equal
    0 4slide ret
.equals_len_equal:
    drop swap ^2 + 1- # addr1+len-1
    ^2 ^2 # i, j = addr1, addr2
.equals_loop:
    ^2 ^2 j< .equals_equal # i >= addr1+len
    ^1 retrieve ^1 retrieve
    j= .equals_char_equal
    jmp .equals_not_equal
.equals_char_equal:
    swap 1+ swap 1+ # i++, j++
    jmp .equals_loop
.equals_equal:
    1 5slide ret
.equals_not_equal:
    0 5slide ret

# compare returns the relation between the two strings. Returns 0 if
# both strings are equal. Returns negative if the first differing
# character has a lower value in str1 than in str2. Returns positive
# if the first differing character has a greater value in str1 than in
# str2.
# (addr1 len1 addr2 len2 -- relation)
compare:
    ^3 ^3 + 1- ^2 ^2 + 1- # addr1+len1-1 addr2+len2-1
    ^5 ^4 # i, j = addr1, addr2
.compare_loop:
    ^3 ^2 j< .compare_bounds # i >= addr1+len
    ^2 ^1 j< .compare_bounds # j >= addr2+len
    ^1 retrieve ^1 retrieve
    2dup j= .compare_char_equal
    jmp .compare_ret
.compare_char_equal:
    2drop
    swap 1+ swap 1+ # i++, j++
    jmp .compare_loop
.compare_bounds:
    ^6 ^5 # len1 len2
.compare_ret:
    - 8slide ret

# less returns whether str1 compares less than str2.
# (addr1 len1 addr2 len2 -- ?)
less:
    call compare neg? ret

# has_prefix returns whether the string starts with the prefix.
# (addr len prefixaddr prefixlen -- ?)
has_prefix:
    -1 # i
    ^3 ^2 j< .has_prefix_false
.has_prefix_loop:
    1+
    2dup j<= .has_prefix_true # i >= prefixlen
    ^4 ^1 + retrieve ^3 ^2 + retrieve
    j= .has_prefix_loop
.has_prefix_false:
    0 5slide ret
.has_prefix_true:
    1 5slide ret

# has_suffix returns whether the string ends with the suffix.
# (addr len suffixaddr suffixlen -- ?)
has_suffix:
    ^3 ^3 + ^2 ^2 + # addr+len suffixaddr+suffixlen
    0 # i
    ^5 ^4 j< .has_suffix_false
.has_suffix_loop:
    2dup j<= .has_suffix_true # i >= suffixlen
    1+
    ^2 ^1 - retrieve ^2 ^2 - retrieve
    j= .has_suffix_loop
.has_suffix_false:
    0 7slide ret
.has_suffix_true:
    1 7slide ret
